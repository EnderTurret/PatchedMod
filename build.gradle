plugins {
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.7'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'

    version = '6.0.0+1.20.5'
    group = 'net.enderturret'

    ext {
        patchedVersion = '1.4.0'
    }

    java.toolchain.languageVersion = JavaLanguageVersion.of(21)
    java.withSourcesJar()

    repositories {
        maven {
            url = 'https://jitpack.io'
            content {
                includeGroup 'com.github.EnderTurret'
            }
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
    }

    tasks.withType(GenerateModuleMetadata).configureEach {
        enabled = false
    }

    tasks.withType(AbstractArchiveTask).configureEach {
        preserveFileTimestamps = false
        reproducibleFileOrder = true
    }

    tasks.named('jar', Jar).configure {
        manifest {
            attributes([
                'Specification-Title': 'Patched',
                'Specification-Vendor': 'EnderTurret',
                'Specification-Version': '1',
                'Implementation-Title': 'Patched',
                'Implementation-Version': "${version}",
                'Implementation-Vendor': 'EnderTurret'
            ])
        }
    }

    tasks.named('processResources', ProcessResources).configure {
        doLast {
            fileTree(dir: outputs.files.asPath, include: '**/*.json').each {
                File file -> file.text = groovy.json.JsonOutput.toJson(new groovy.json.JsonSlurper().parse(file))
            }
        }
    }
}