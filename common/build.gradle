plugins {
    id 'org.spongepowered.gradle.vanilla' version '0.2.1-SNAPSHOT'
}

base {
    archivesName = 'Patched-common'
}

minecraft {
    version '1.20.1'
    accessWideners "$rootDir/common/patched_common.accesswidener"
}

configurations {
    shadow
}

dependencies {
    compileOnly 'org.spongepowered:mixin:0.8.5'

    compileOnly(shadow("com.github.EnderTurret:Patched:$patchedVersion")) {
        exclude group: 'com.google.code.gson', module: 'gson'
    }

    compileOnly(annotationProcessor('io.github.llamalad7:mixinextras-common:0.4.1'))
}

tasks.named('jar', Jar).configure {
    // Add the Patched library to the built common jar.
    dependsOn configurations.shadow
    from {
        configurations.shadow.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}

tasks.named('shadowJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar).configure {
    enabled = false
}

genEclipseRuns.dependsOn eclipseClasspath
genEclipseRuns.dependsOn eclipseProject
genEclipseRuns.dependsOn eclipseJdt
genEclipseRuns.dependsOn eclipseFactorypath
genEclipseRuns.dependsOn eclipseJdtApt