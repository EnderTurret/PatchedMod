// https://projects.neoforged.net/
plugins {
    id 'java-library'
    id 'net.neoforged.moddev' version '1.0.21'
}

base {
    archivesName = 'Patched-neoforge'
}

configurations {
    shadow {}
    minecraftLibrary {}
    implementation.extendsFrom minecraftLibrary
}

dependencies {
    implementation (project(':PatchedModCommon')) {
        exclude group: 'org.spongepowered', module: 'mixin'
        exclude group: 'net.minecraft', module: 'joined_aw'
    }

    shadow (minecraftLibrary("com.github.EnderTurret:Patched:$patchedVersion")) {
        exclude group: 'com.google.code.gson', module: 'gson'
    }
    sourceShadow ("com.github.EnderTurret:Patched:$patchedVersion:sources") {
        exclude group: 'com.google.code.gson', module: 'gson'
    }
}

neoForge {
    version = '21.1.72'

    mods {
        patched {
            sourceSet sourceSets.main
            sourceSet project(':PatchedModCommon').sourceSets.main
        }
    }

    parchment {
        // https://parchmentmc.org/docs/getting-started
        // https://ldtteam.jfrog.io/ui/native/parchmentmc-public/org/parchmentmc/data/
        minecraftVersion = '1.21'
        mappingsVersion = '2024.07.28'
    }

    runs {
        configureEach {
            additionalRuntimeClasspathConfiguration.extendsFrom configurations.minecraftLibrary
        }

        client {
            client()
            ideName = 'runPatchedModNeoForgeClient'
            gameDirectory = project.file('run/1.21')
        }
        server {
            server()
            ideName = 'runPatchedModNeoForgeServer'
        }
        data {
            data()
            ideName = 'runPatchedModNeoForgeData'
        }
    }
}

tasks.named('shadowJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar).configure {
    archiveClassifier = ''

    configurations = [project.configurations.shadow]

    from (rootDir) {
        include 'LICENSE.txt'
    }
}

jar.enabled = false

artifacts {
    archives shadowJar
    shadow shadowJar
}