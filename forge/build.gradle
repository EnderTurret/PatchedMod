plugins {
    id 'net.minecraftforge.gradle' version '6.0.29'
    id 'org.spongepowered.mixin' version '0.7.38'
    id 'org.parchmentmc.librarian.forgegradle' version '1.2.0'
}

base {
    archivesName = 'Patched-forge'
}

mixin {
    add sourceSets.main, 'patched.refmap.json'
    quiet
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.20.1-47.3.11'

    compileOnly (project(':PatchedModCommon')) {
        exclude group: 'org.spongepowered', module: 'mixin'
        exclude group: 'net.minecraft', module: 'joined_aw'
    }

    minecraftLibrary ("com.github.EnderTurret:Patched:$patchedVersion") {
        exclude group: 'com.google.code.gson', module: 'gson'
    }

    compileOnly(annotationProcessor('io.github.llamalad7:mixinextras-common:0.4.1'))
    implementation(jarJar('io.github.llamalad7:mixinextras-forge:0.4.1')) {
        jarJar.ranged(it, '[0.4.1,)')
    }

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

minecraft {
    mappings channel: 'parchment', version: '2023.09.03-1.20.1'
    accessTransformer = file("$rootDir/forge/src/main/resources/META-INF/accesstransformer.cfg")

    runs {
        configureEach {
            workingDirectory project.file('run')

            arg '-mixin.config=mixins.patched.json'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                patched {
                    source sourceSets.main
                    source project(':PatchedModCommon').sourceSets.main
                }
            }
        }

        client {
            taskName 'runPatchedClientForge'
        }

        server {
            taskName 'runPatchedServerForge'
        }

        data {
            taskName 'runPatchedDataForge'

            args '--mod', 'patched', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
            'MixinConfigs': 'mixins.patched.json'
        ])
    }
    enabled = false
}

reobf {
    shadowJar {
        enabled = false
    }
    jar {
        enabled = false
    }
}

tasks.named('shadowJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar).configure {
    archiveClassifier = 'deobf-shadow-delete-me'

    dependencies {
        include(dependency("com.github.EnderTurret:Patched:$patchedVersion"))
    }
}

// Alright, so this is kind of terrible, but it's the only way
// I can see to get jarJar to use the output of shadowJar.
tasks.named('jarJar', net.minecraftforge.gradle.userdev.tasks.JarJar).configure {
    archiveClassifier = ''
    from zipTree(shadowJar.archiveFile)
}

// Fix mixingradle.
//eclipseJdtApt.dependsOn createMcpToSrg