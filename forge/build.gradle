plugins {
    id 'eclipse'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'net.minecraftforge.gradle' version '[6.0, 6.2)'
    id 'org.spongepowered.mixin' version '0.7.33'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

base {
    archivesName = 'Patched-forge'
}

mixin {
    add sourceSets.main, 'patched.refmap.json'
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.20.1-47.0.0'

    compileOnly project(':PatchedModCommon')

    minecraftLibrary ('com.github.EnderTurret:Patched:1.2.1') {
        exclude group: 'com.google.code.gson', module: 'gson'
    }

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

minecraft {
    mappings channel: 'parchment', version: '1.19.3-2023.03.12-1.20.1'
	accessTransformer = file("$rootDir/forge/src/main/resources/META-INF/accesstransformer.cfg")

    runs {
        client {
            workingDirectory project.file('run')
            taskName 'runPatchedClientForge'

            arg '-mixin.config=mixins.patched.json'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                patched {
                    source sourceSets.main
                    source project(':PatchedModCommon').sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            taskName 'runPatchedServerForge'

            arg '-mixin.config=mixins.patched.json'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                patched {
                    source sourceSets.main
                    source project(':PatchedModCommon').sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            taskName 'runPatchedDataForge'

            arg '-mixin.config=mixins.patched.json'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            args '--mod', 'patched', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                patched {
                    source sourceSets.main
                    source project(':PatchedModCommon').sourceSets.main
                }
            }
        }
    }
}

compileJava {
    source project(':PatchedModCommon').sourceSets.main.allSource
}

sourcesJar {
    from project(':PatchedModCommon').sourceSets.main.allJava
}

processResources {
    from project(':PatchedModCommon').sourceSets.main.resources
}

reobf {
    shadowJar {
        mappings = createMcpToSrg.output
    }
    jar {
        enabled = false
    }
}

shadowJar {
    archiveClassifier = ''

    dependencies {
        include(dependency('com.github.EnderTurret:Patched:1.2.1'))
    }
}

jar.enabled = false

jar {
    manifest {
        attributes([
            'Specification-Title': 'Patched',
            'Specification-Vendor': 'EnderTurret',
            'Specification-Version': '1',
            'Implementation-Title': 'Patched',
            'Implementation-Version': "${version}",
            'Implementation-Vendor' : 'EnderTurret',
            'MixinConfigs': 'mixins.patched.json'
        ])
    }
}

shadowJar.finalizedBy('reobfShadowJar')

// Fix mixingradle.
eclipseJdtApt.dependsOn createMcpToSrg

import groovy.json.*

processResources {
    doLast {
        fileTree(dir: outputs.files.asPath, include: '**/*.json').each {
            File file -> file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
        }
    }
}