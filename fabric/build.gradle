plugins {
    // https://maven.fabricmc.net/net/fabricmc/fabric-loom/
    id 'fabric-loom' version '1.7.4'
}

base {
    archivesName = 'Patched-fabric'
}

configurations {
    implementation.extendsFrom shadow
}

// https://fabricmc.net/develop/
dependencies {
    minecraft 'com.mojang:minecraft:1.20.1'
    mappings loom.officialMojangMappings()

    modImplementation 'net.fabricmc:fabric-loader:0.16.7'
    modImplementation 'net.fabricmc.fabric-api:fabric-api:0.92.2+1.20.1'

    shadow ("com.github.EnderTurret:Patched:$patchedVersion") {
        exclude group: 'com.google.code.gson', module: 'gson'
    }
    sourceShadow ("com.github.EnderTurret:Patched:$patchedVersion:sources") {
        exclude group: 'com.google.code.gson', module: 'gson'
    }

    compileOnly (project(':PatchedModCommon')) {
        exclude group: 'org.spongepowered', module: 'mixin'
        exclude group: 'net.minecraft', module: 'joined_aw'
    }
}

loom {
    accessWidenerPath = file("$rootDir/fabric/src/main/resources/patched.accesswidener")

    mixin {
        defaultRefmapName = 'patched.refmap.json'
    }

    // Due to an incredible oversight(?) in Loom, these actually do nothing.
    // You cannot change the configuration name from the default, which is derived from the project name.
    runs {
        client {
            configName = 'runPatchedClientFabric'
        }

        server {
            configName = 'runPatchedServerFabric'
        }
    }

    runConfigs.configureEach {
        ideConfigGenerated = true
    }
}

tasks.named('shadowJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar).configure {
    configurations = [project.configurations.shadow]
}

tasks.register('remappedShadowJar', net.fabricmc.loom.task.RemapJarTask) {
    archiveClassifier = ''
    input = tasks.shadowJar.archiveFile
    dependsOn shadowJar
    from (rootDir) {
        include 'LICENSE.txt'
    }
}

artifacts {
    archives remappedShadowJar
    shadow shadowJar
}

tasks.named('processResources', ProcessResources).configure {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand 'version': project.version
    }
}