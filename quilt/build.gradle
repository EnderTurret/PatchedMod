plugins {
    id 'com.gradleup.shadow' version '8.3.0'
    id 'org.quiltmc.loom' version '1.7.4'
}

base {
    archivesName = 'Patched-quilt'
}

configurations {
    implementation.extendsFrom shadow
}

// https://quiltmc.org/en/usage/latest-versions
dependencies {
    minecraft 'com.mojang:minecraft:1.20.1'
    mappings loom.officialMojangMappings()

    modImplementation 'org.quiltmc:quilt-loader:0.26.4'
    modImplementation 'org.quiltmc:qsl:6.2.0+1.20.1'

    shadow ("com.github.EnderTurret:Patched:$patchedVersion") {
        exclude group: 'com.google.code.gson', module: 'gson'
    }

    compileOnly (project(':PatchedModCommon')) {
        exclude group: 'org.spongepowered', module: 'mixin'
        exclude group: 'net.minecraft', module: 'joined_aw'
    }
}

loom {
    accessWidenerPath = file("$rootDir/quilt/src/main/resources/patched.accesswidener")

    mixin {
        defaultRefmapName = 'patched.refmap.json'
    }

    // Due to an incredible oversight(?) in Loom, these actually do nothing.
    // You cannot change the configuration name from the default, which is derived from the project name.
    runs {
        client {
            configName = 'runPatchedClientQuilt'
        }

        server {
            configName = 'runPatchedServerQuilt'
        }
    }

    runConfigs.configureEach {
        ideConfigGenerated = true
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
            'Specification-Title': 'Patched',
            'Specification-Vendor': 'EnderTurret',
            'Specification-Version': '1',
            'Implementation-Title': 'Patched',
            'Implementation-Version': "${version}",
            'Implementation-Vendor': 'EnderTurret'
        ])
    }
}

tasks.named('shadowJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar).configure {
    configurations = [project.configurations.shadow]
}

tasks.named('compileJava', JavaCompile).configure {
    source project(':PatchedModCommon').sourceSets.main.allSource
}

tasks.named('sourcesJar', Jar).configure {
    from project(':PatchedModCommon').sourceSets.main.allJava
}

tasks.named('processResources', ProcessResources).configure {
    from project(':PatchedModCommon').sourceSets.main.resources
}

import net.fabricmc.loom.task.RemapJarTask

tasks.register('remappedShadowJar', RemapJarTask) {
    archiveClassifier = ''
    input = tasks.shadowJar.archiveFile
    dependsOn shadowJar
}

artifacts {
    archives remappedShadowJar
    shadow shadowJar
}

processResources {
    inputs.property 'version', project.version

    filesMatching('quilt.mod.json') {
        expand 'version': project.version
    }
}